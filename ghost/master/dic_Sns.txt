



＃初回起動→ファイル読み込み→起動時モノローグ
＃通常起動→ファイル読み込み→ユーザオフパコレポ生成→呟き生成→ファイル書き込み→ファイル読み込み→起動時モノローグ



＃＜呟きファイル読み込み＞　---------------------------------------------

＊ファイル読み込み
＄filepath	..\tweet.txt
＄loop	1
＞FOPEN失敗	！（aya、FOPEN、（filepath）、r）
＞ファイル読み込みループ

＊ファイル読み込みループ
＄txt	（aya、FREAD、（filepath））
＞ファイル読み込みループ終了	！（変数「txt」の存在）｜｜（loop）＞６０
＄呟きデータ（zen2han、（loop））	（txt）
＄loop=（loop）+1
＞ファイル読み込みループ

＊ファイル読み込みループ終了
＄最終呟きデータ数＝（loop）-1
（aya、FCLOSE、（filepath））
＞起動時モノローグ	（呟き生成済み）＝＝１｜｜（初回起動フラグ）＝＝１
＞ユーザオフパコレポ生成

＊FOPEN失敗
：ファイルのオープンに失敗しました。 


＃＜呟きファイル書き出し＞　---------------------------------------------

＊ファイル書き出し
＄filepath	..\tweet.txt
＞FOPEN失敗	!（aya、FOPEN、（filepath）、w）
（for、（書き込み開始値）、（書き込み終了値）、
	（aya、FWRITE、（filepath）、（呟きデータ（zen2han、（Ｃ０））））
）（aya、FCLOSE、（filepath））
＞ファイル読み込み


＠合計呟き数
（calc、（最終呟きデータ数）＋（追加呟き数））

＠書き込み開始値
（when、（合計呟き数）＞６０、（calc、（合計呟き数）−５９）、１）

＠書き込み終了値
（合計呟き数）



＃＜新規呟き作成＞　---------------------------------------------

＊新規呟き生成
＄呟き生成済み＝１
＄雑談回数＝（乱数０〜４）
＄オフパコ実行回数＝（乱数０〜２）
（when、（オフパコ実行回数）＞０、
	（for、１、（オフパコ実行回数）、
		（when、（call、雑談挿入判定）＝＝１、（set、呟きデータ（zen2han、（calc、（最終呟きデータ数）＋（カウンタ）））、（合成単語群、健全呟き、軽エロ系呟き））（set、カウンタ、（calc、（カウンタ）+1）））φ
		（set、呟きデータ（zen2han、（calc、（最終呟きデータ数）＋（カウンタ）））、（オフパコ募集））（set、カウンタ、（calc、（カウンタ）+1））φ
		（when、（call、雑談挿入判定）＝＝１、（set、呟きデータ（zen2han、（calc、（最終呟きデータ数）＋（カウンタ）））、（合成単語群、健全呟き、軽エロ系呟き））（set、カウンタ、（calc、（カウンタ）+1）））φ
		（when、（乱数０〜２）＞＝１、（set、呟きデータ（zen2han、（calc、（最終呟きデータ数）＋（カウンタ）））、（オフパコ直前呟き））（set、カウンタ、（calc、（カウンタ）+1）））φ
		（set、呟きデータ（zen2han、（calc、（最終呟きデータ数）＋（カウンタ）））、（オフパコ呟き））（set、カウンタ、（calc、（カウンタ）+1））
	）
）φ
（when、（雑談回数）＞０、
	（for、（雑談回数）、０、−１、
		（set、呟きデータ（zen2han、（calc、（最終呟きデータ数）＋（カウンタ）））、（合成単語群、健全呟き、軽エロ系呟き））（set、カウンタ、（calc、（カウンタ）+1））
	）
）φ
（set、呟きデータ（zen2han、（calc、（最終呟きデータ数）＋（カウンタ）））、（オフパコ募集））（set、カウンタ、（calc、（カウンタ）+1））

＄追加呟き数＝（カウンタ）−１
＞ファイル書き出し


＠雑談挿入判定
（when、（雑談回数）＞０＆＆（乱数０〜２）＝＝１、１、０）


＃＜ユーザオフパコレポ生成＞　---------------------------------------------

＊ユーザオフパコレポ生成
（whenlist、（フェーズ）
、＝＝１、（set、カウンタ、（calc、（カウンタ）+1））（set、呟きデータ（zen2han、（calc、（最終呟きデータ数）＋（カウンタ）））、（待ち合わせ無視））（set、カウンタ、（calc、（カウンタ）+1））
、＜＝６、（set、カウンタ、（calc、（カウンタ）+1））（set、呟きデータ（zen2han、（calc、（最終呟きデータ数）＋（カウンタ）））、（途中帰宅））（set、カウンタ、（calc、（カウンタ）+1））
、（set、呟きデータ（zen2han、（calc、（最終呟きデータ数）＋（カウンタ）））、（童貞生オフパコ呟き））（set、カウンタ、（calc、（カウンタ）+1））
）
＞新規呟き生成


＃＜呟き表示画面＞　---------------------------------------------


＊SNSメニュー
＄SNS初回観覧済み＝１
＞呟き画面初期化処理

＊呟き画面初期化処理
＄表示中呟きページ＝０
＄最大呟きページ＝（calc、（calc、（最終呟きデータ数）−１）÷１０）
＞呟き表示画面


＊呟き表示画面
\*\_q\1\b[2]（プロフィール表示）\0\b[2]（for、（ループ開始値）、（ループ終了値）、−１、
	（when、（length、（呟きデータ（zen2han、（Ｃ０））））＞１、
		（call、呟き表示、（呟きデータ（zen2han、（Ｃ０））））
	）
）
（call、呟き左矢印）\_l[75,@0]（call、呟き右矢印）\_l[180,@0]\q[見るのをやめる,起動時メニュー]

\_q


＠ループ開始値
（calc、（最終呟きデータ数）−（calc、（表示中呟きページ）×１０））

＠ループ終了値
（when、（calc、（ループ開始値）−９）＜＝０、1、（calc、（ループ開始値）−９））

＠呟き左矢印
（when、（表示中呟きページ）＞＝１、\q[←前のページ,呟きページ戻り]、　）

＠呟き右矢印
（when、（表示中呟きページ）＜（最大呟きページ）、\q[次のページ→,呟きページ進み]、　）

＊呟きページ戻り
＄表示中呟きページ＝（表示中呟きページ）−１
＞呟き表示画面

＊呟きページ進み
＄表示中呟きページ＝（表示中呟きページ）＋１
＞呟き表示画面

＃＜プロフィール表示＞　---------------------------------------------

＠プロフィール表示
（プロフィールアイコン表示）（プロフィールアカウント名表示）\n\_l[0,@10]（プロフィール詳細）\n\n[50]（フォローフォロワー欄表示）



＠プロフィールアイコン表示
\_b[..\\..\\shell\\master\icon\\ProfileIcon.png,inline,opaque]




＠プロフィールアカウント名表示
\_l[52,@0]\f[bold,true]\f[height,120%]（アカウント名）\f[bold,default]\f[height,default] \_l[52,@21]（アカウントID）

＠フォローフォロワー欄表示
（フォロー数） フォロー\_l[100,@0]（フォロワー数） フォロワー

＠フォロー数
34

＠フォロワー数
21345

＠プロフィール詳細
女子大生/成人済み/Nカップ/男の子大好きｈ\nえっちなこと大好きhオフパコ募集とオフパコレポ投稿します★　オフパコ相手はフォロワー限定\n絶倫クンやドーテークン大歓迎ｈ　繋がりたい男の子は気軽に声かけて§

＠アカウント名
めるみぃ＠童貞スキｈ

＠アカウントID
@mememe_ns


＃＜呟き表示＞　---------------------------------------------

＃引数１：呟き本文　引数２：リプライ、リツイート、いいね（区切り文字は｜）
＠呟き表示
（nop、（split、（Ａ０）、｜））（罫線）\n[80]（アイコン表示）（アカウント名表示）\n\_l[0,@3]（Ｓ０）\n（call、リプライその他欄表示、（Ｓ１）、（Ｓ２）、（Ｓ３））\n

＠罫線
――――――――――――――――――――――――

＠アイコン表示
\_b[..\\..\\shell\\master\icon\\TweetIcon.png,inline,opaque]

＠アカウント名表示
\_l[30,@0]（アカウント名）\_l[30,@14]（アカウントID）

＠リプライその他欄表示
\f[name,メイリオ]（リプライ） （zen2han、（Ａ０））\_l[70,@0]（リツイート） （zen2han、（Ａ１））\_l[140,@0]（いいね） （zen2han、（Ａ２））\f[name,default]


＠リプライ
\_u[0x27A5]

＠リツイート
\_u[0x21BA]

＠いいね
\_u[0x2661]

